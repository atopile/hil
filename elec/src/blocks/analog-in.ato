from "components/amplifier.ato" import Amplifier
from "components/tvs.ato" import TVS
from "generics/vdivs.ato" import VDiv
from "components/ads1115.ato" import ADS1115IDGSR
from "generics/interfaces.ato" import Power, I2C, Pair
from "components/dowo_smf33a.ato" import DOWO_SMF3_3A
from "blocks/isolator-i2c.ato" import IsolatorI2C
from "components/ylptec_b0505s2wr3.ato" import YLPTEC_B0505S_2WR3
from "ldk220m-r/elec/src/ldk220m-r.ato" import LDK220M_R

module AnalogIn:
    """
    Analog input block
    - ADC
    - Amplifier
    - TVS
    - VDiv
    """
    # External Interfaces
    power_3v3 = new Power
    i2c = new I2C
    input = new Pair

    # Components
    adc = new ADS1115IDGSR
    amplifier = new Amplifier
    tvs = new TVS
    vdiv = new VDiv

    tvs.diode -> DOWO_SMF3_3A

    # Power
    power_3v3 ~ adc.power
    power_3v3 ~ amplifier.power_in

    # Connections
    input.io ~ vdiv.top
    input.gnd ~ vdiv.bottom
    input.gnd ~ power_3v3.gnd
    vdiv.out ~ tvs.input.io
    tvs.output ~ amplifier.input
    amplifier.output.io ~ adc.AIN0
    adc.i2c ~ i2c

    # Protection
    vdiv.out ~ tvs.input.io
    power_3v3.gnd ~ tvs.input.gnd

    # Power
    power_3v3 ~ amplifier.power_in
    power_3v3 ~ adc.power


module AnalogInIsolated:
    """
    Analog input block with isolated power supply
    """
    # External Interfaces
    i2c = new I2C
    power_5v = new Power
    power_3v3 = new Power
    input = new Pair

    # Components
    analog_in = new AnalogIn
    i2c_isolator = new IsolatorI2C
    regulator_5v_isolated = new YLPTEC_B0505S_2WR3
    regulator_3v3_isolated = new LDK220M_R

    # Internal power
    _power_5v_isolated = new Power
    _power_3v3_isolated = new Power

    # I2C
    i2c ~ i2c_isolator.i2c_non_isolated
    i2c_isolator.i2c_isolated ~ analog_in.i2c

    # Power
    power_5v ~ regulator_5v_isolated.power_in
    regulator_5v_isolated.power_out ~ _power_5v_isolated
    _power_5v_isolated ~ regulator_3v3_isolated.power_in
    regulator_3v3_isolated.power_out ~ _power_3v3_isolated
    _power_3v3_isolated ~ analog_in.power_3v3
    _power_3v3_isolated ~ i2c_isolator.power_isolated
    power_3v3 ~ i2c_isolator.power_non_isolated

    # Connections  
    input ~ analog_in.input
