import ElectricSignal

from "generics/opamps.ato" import Opamp
from "generics/interfaces.ato" import Power
from "generics/capacitors.ato" import Capacitor
from "generics/resistors.ato" import Resistor
from "components/texas_instruments_lm358d_r.ato" import Texas_Instruments_LM358DR

module Amplifier:
    """
    Amplifies the input signal by a given gain.
    Uses a Non-Inverting Opamp and a feedback resistor divider.
    """
    power_in = new Power
    input = new ElectricSignal
    output = new ElectricSignal

    feedback_resistor = new Resistor
    input_resistor = new Resistor
    opamp = new Opamp

    # Decoupling Capacitor
    decoupling_capacitor = new Capacitor
    decoupling_capacitor.package = "0402"
    decoupling_capacitor.capacitance = 1uF +/- 20%
    power_in ~ decoupling_capacitor.power

    feedback_resistor.package = "0402"
    input_resistor.package = "0402"

    opamp -> Texas_Instruments_LM358DR

    # Connections
    power_in ~ opamp.power
    input.line ~ opamp.non_inverting
    power_in.gnd ~ input_resistor.p1; input_resistor.p2 ~ opamp.inverting
    opamp.inverting ~ feedback_resistor.p1; feedback_resistor.p2 ~ opamp.output
    output.line ~ opamp.output

    # Common gnd
    power_in ~ input.reference
    power_in ~ output.reference

    # Parameters
    gain: dimensionless

    # # Configure the amplifier
    assert 1 + feedback_resistor.resistance / input_resistor.resistance is gain
    assert output.voltage is gain * input.voltage
    assert output.voltage <= opamp.supply_voltage
