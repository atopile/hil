from "generics/interfaces.ato" import Power
from "generics/resistors.ato" import Resistor
from "generics/interfaces.ato" import Power, DiffPair

module CurrentSensor:
    sensor = new INA185A2IDRLR
    shunt = new Resistor
    power = new Power
    power_in = new Power
    power_out = new Power
    signal output ~ sensor.out
    current: current

    # power_in.current += power_out.current
    # power_in.voltage &= power_out.voltage - power_in.current * shunt.value

    # Shunt config
    assert shunt.value * current * sensor.gain within 1.2V to 3V
    shunt.package = "0805"

    # Shunt connections
    power_in.vcc ~ shunt.p1; shunt.p2 ~ power_out.vcc
    power_in.gnd ~ power_out.gnd

    # Sensor connections
    sensor.input.p ~ shunt.p1
    sensor.input.n ~ shunt.p2

    sensor.ref ~ power_in.gnd

    # Power connections
    power ~ sensor.power

component INA185A2IDRLR:
    # component NTZD3154NT1G
    footprint = "SOT-563_L1.6-W1.2-P0.50-LS1.6-TL"
    lcsc_id = "C2059320"
    mpn = "C2059320"
    gain = 50

    power = new Power
    power.vcc ~ pin 6
    power.gnd ~ pin 2

    signal out ~ pin 1
    signal ref ~ pin 5

    input = new DiffPair
    input.p ~ pin 3
    input.n ~ pin 4
