import ElectricSignal

from "generics/resistors.ato" import Resistor
from "generics/interfaces.ato" import Power, DiffPair


module CurrentSensor:
    sensor = new INA185A2IDRLR
    shunt = new Resistor
    power = new Power
    power_in = new Power
    power_out = new Power
    output = new ElectricSignal

    max_current: current  # The designed current limit to measure to

    sensor.out ~ output.line

    # Shunt config
    assert shunt.value * max_current * sensor.gain within 2.5V to 3V
    # 1.5x -> 50% margin on the the power rating is expected
    assert max_current ** 2 * shunt.value * 1.5 <= shunt.max_power
    shunt.package = "0805"

    # Shunt connections
    power_in.vcc ~ shunt.p1; shunt.p2 ~ power_out.vcc
    power_in.gnd ~ power_out.gnd

    # Sensor connections
    sensor.input.p ~ shunt.p1
    sensor.input.n ~ shunt.p2

    sensor.ref ~ power_in.gnd

    # Power connections
    power ~ sensor.power


component INA185A2IDRLR:
    """Current sense amplifier IC"""
    footprint = "SOT-563_L1.6-W1.2-P0.50-LS1.6-TL"
    lcsc_id = "C2059320"

    # Fixed gain provided by this variant
    gain = 50

    power = new Power
    power.vcc ~ pin 6
    power.gnd ~ pin 2

    signal out ~ pin 1
    signal ref ~ pin 5

    input = new DiffPair
    input.p ~ pin 3
    input.n ~ pin 4
