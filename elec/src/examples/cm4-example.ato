from "blocks/controller-cm4.ato" import ControllerCM4
from "blocks/coms-can.ato" import ComsCANISO
from "blocks/power-usbpd.ato" import PowerUSBPD
from "blocks/analog-in.ato" import AnalogIn, AnalogInIsolated
from "components/tca9548apwr/elec/src/tca9548apwr.ato" import TCA9548APWR
from "components/lv2842xlvddcr/elec/src/lv2842kit.ato" import LV2842Kit
from "components/dosinconn_do_sin8010072.ato" import dosinconn_DOSIN_801_0072
from "ldk220m-r/elec/src/ldk220m-r.ato" import LDK220M_R
from "generics/interfaces.ato" import Power
from "jst-gh/elec/src/jst-gh.ato" import JSTGH4Pin

module CM4Example:
    """
    Example board with CM4 processor
    - Ethernet interface
    - CAN interface
    - Power supply (USBPD)
    - ADI ISO-SPI interface
    - Analog Input (High Speed ADC)
    """
    # Power
    usb_pd = new PowerUSBPD
    regulator_3v3 = new LDK220M_R
    regulator_5v = new LV2842Kit

    # Configure power
    regulator_5v.v_out = 5V +/- 5%
    regulator_3v3.v_out = 3.3V +/- 5%

    # Internal Power rails
    power_usb = new Power
    power_3v3 = new Power
    power_5v = new Power

    # Components
    controller = new ControllerCM4
    power_5v ~ controller.power_5v

    # I2C Multiplexer
    i2c_multiplexer = new TCA9548APWR
    controller.i2c ~ i2c_multiplexer.i2c
    power_3v3 ~ i2c_multiplexer.power

    # Analog Inputs
    analog_in_1 = new AnalogIn
    analog_in_1_connector = new dosinconn_DOSIN_801_0072
    i2c_multiplexer.i2c0 ~ analog_in_1.i2c
    analog_in_1_connector.input ~ analog_in_1.input
    power_3v3 ~ analog_in_1.power_3v3

    analog_in_2 = new AnalogInIsolated
    analog_in_2_connector = new dosinconn_DOSIN_801_0072
    i2c_multiplexer.i2c4 ~ analog_in_2.i2c
    analog_in_2_connector.input ~ analog_in_2.input
    power_5v ~ analog_in_2.power_5v
    power_3v3 ~ analog_in_2.power_3v3

    # Common gnd
    power_usb.gnd ~ power_3v3.gnd
    power_3v3.gnd ~ power_5v.gnd

    # Power Regulator connections
    usb_pd.power_out ~ power_usb
    power_usb ~ regulator_5v.power_in
    regulator_5v.power_out ~ power_5v
    power_5v ~ regulator_3v3.power_in
    regulator_3v3.power_out ~ power_3v3

    # USBPD
    controller.i2c ~ usb_pd.i2c

    # USB Connector (External programmers etc)
    usb_connector = new XKB_Connectivity_U262_241N_4BV64
    controller.usb2 ~ usb_connector.usb2
    power_5v ~ usb_connector.power_5v

    # CAN Interface
    can_interface = new ComsCANISO
    can_interface.power_3v3 ~ power_3v3
    can_interface.power_5v ~ power_5v
    controller.spi4 ~ can_interface.spi

    can_connector = new JSTGH4Pin
    can_interface.can ~ can_connector.can
    can_interface._power_5v_iso ~ can_connector.power
